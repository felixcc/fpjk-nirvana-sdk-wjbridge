apply plugin: 'com.android.library'
//Gradle Android Maven plugin
apply plugin: 'com.github.dcendents.android-maven'
//Gradle Bintray Plugin
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'             //fastJson
        exclude 'META-INF/NOTICE.txt'              //fastJson
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
}

//定义公共参数
def siteUrl = 'https://github.com/felixcc/fpjk-nirvana-sdk-wjbridge'   // 项目的主页
def gitUrl = 'https://github.com/felixcc/fpjk-nirvana-sdk-wjbridge.git'   // Git仓库的url

//配置android-maven-gradle-plugin插件
group = 'fpjk.nirvana.sdk.wjbridge'   // groupId,一般由host+artifactId组成
version = '0.0.5'  //版本号,以后更新library时,它也要跟着增加

//配置android-maven-gradle-plugin插件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'fpjk-nirvana-sdk-wjbridge'     //项目的名称
                description 'a powerful nirvana for android'  //项目的描述
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'noblesky'        //bintray的username
                        name 'felixchen'          //姓名
                        email 'lovejiuwei@gmail.com'      //邮箱
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//配置gradle-bintray-plugin插件
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

//对gradle-bintray-plugin插件的配置,全部写在 bintray{ ... } 中
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']

    pkg {
        repo = "maven"
        name = "nirvana"    //artifactId,会作为library的名称显示在JCenter中
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true

        version {
            desc = 'a powerful nirvana for android'
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.2.0'
    //fastjson
    compile 'com.alibaba:fastjson:1.2.21'
    //rxjava
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.2'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    compile 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
}